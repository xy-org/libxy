import libc~[Clib{headers=@{"string.h", "stdio.h", "inttypes.h"}}] in c;
import libxy.memory;
import libxy.error;
import libxy.slice;
import libxy.bitwise;

def cstr~[StrCtor{prefix="c", interpolation=false}] (
    addr: Ptr, size: Size
)
    addr~[c.char];

;; Tag for strings that are managed by the Str struct itself.
;; Have trailing zero byte.
struct MemManaged {}

;; Tag for strings that are not managed by the Str struct itself.
;; No assumptions about the existence of a trailing zero byte.
struct MemExt {}

;; Tag for strings that are are allocated in static memory and need no free.
;; Have trailing zero byte.
struct MemStatic {}


;; A UTF-8 encoded string
*struct Str~[memory :struct = MemManaged] {
    addr: Ptr~Byte;
    len: Size;
}

*def dtor(str: Str) dtor(str, str..memory);

def dtor(str: Str, :MemManaged) {
    c.free(str.addr);
}

def dtor(str: Str, :MemExt) {}

def dtor(str: Str, :MemStatic) {}


def str~[StrCtor{prefix="", interpolation=false}] (
    addr: Ptr, size: Size
) Str~MemStatic{addr, size};

;; The byte-length of the string as encoding in UTF-8
*def len(s: Str) s.len;

*def get(s: Str, i: Size) -> Byte
>> i < s'len
{
    return [(s.addr)~[to=Byte] + i];
}

;; Count the number of unicode code points (chars) in the string
;; Illegal byte sequences are counted as 1 code point
*def count(s: Str) -> Size {
    res : Size;
    i : Size;
    while (i < s.len) {
        leadingOnes := s[i]'bits'countLeadingOnes;
        res++;
        i++;
        if (leadingOnes >= 2 && leadingOnes <= 4) {
            contBytes := min((leadingOnes - 1)'to(Size), s.len - i);
            for (_ in 0:contBytes) {
                contOnes := s[i]'bits'countLeadingOnes;
                if (contOnes != 1 && contOnes <= 4) {
                    # premature end of current sequence. Break b/c
                    # a new sequence is following
                    break;
                }
                # else if {contOnes} == 1 then we have a valid seq
                # if {contOnes} is > 4 then we have a broken byte and we want
                # to skip it
                i++;
            }
        }
        ;; if {leadingOnes == 0} then ascii otherwise broken byte
    }
    return res;
}

*def get(s: Str, int: (IntInterval, SizeInterval)) -> Str~MemExt
>> int.start >= 0 && int.start <= int.end
{
    start := min(int.start'to(Size), s.len);
    end := min(int.end'to(Size), s.len);
    return Str{s.addr + start, end - start};
}

*def empty(s: Str) s.len == 0;

*def cmp(str1: Str, str2: Str) -> Int {
    if (str1.len > str2.len) {
        return 1;
    } elif (str1.len < str2.len) {
        return -1;
    }
    return c.strncmp(str1.addr~[c.char], str2.addr~[c.char], str1.len);
}

*def startswith(str: Str, prefix: Str) -> Bool {
    if (prefix.len > str.len) {
        return false;
    }
    strncmpRes: Int = c.strncmp(str.addr~[c.char], prefix.addr~[c.char], prefix.len);
    return strncmpRes == 0;
}

*def endswith(str: Str, suffix: Str) -> Bool {
    if (suffix.len > str.len) {
        return false;
    }
    strncmpRes: Int = c.strncmp(str.addr~[c.char] + (str.len - suffix.len), suffix.addr~[c.char], suffix.len);
    return strncmpRes == 0;
}

*def ensureNullTerminated(str: Str) ensureNullTerminated(str, str..memory);
def ensureNullTerminated(str: Str, :MemManaged) str;
def ensureNullTerminated(str: Str, :MemStatic) str;
def ensureNullTerminated(str: Str, :MemExt) str'copy();

*def nullTerminated(str: Str) nullTerminated(str, str..memory);
def nullTerminated(str: Str, :MemManaged) true;
def nullTerminated(str: Str, :MemStatic) true;
def nullTerminated(str: Str, :MemExt) false;

*def copy(str: Str) -> Str {
    newmem : Memory;
    newmem'realloc(str.len + 1);
    c.memcpy(newmem.addr, str.addr, str.len);
    [newmem.addr + str.len] = 0;; add terminating \0
    return Str{addr=newmem.addr, len=str.len};
}

struct CharIter {
    i: Size;; byte-offset of the code pointer after {char} in string
    char: Char;; character at that offset
}

*def get(str: Str, charIter: CharIter) = charIter.char;

;; Iterate of the unicode characters (code points) in {str}
;; Illegal byte sequences are replaced by U+FFFD
*def chars~IterCtor(str: Str) -> str[ CharIter ] {
    iter : CharIter;
    next(str, iter);
    return iter;
}

*def valid(str: Str, iter: CharIter) iter.char'to(Int) >= 0;

*def next(str: Str, iter: mut CharIter) {
    cp := decode(str, iter.i);
    iter.char = cp;
}

def decode(s: Str, i: mut Size) -> Char {
    if (i >= s.len) {
        return (-1)'to(Char);
    }

    cp : Int;
    leadingByte := s[i];
    leadingOnes := leadingByte'bits'countLeadingOnes;
    i++;

    if (leadingOnes >= 2 && leadingOnes <= 4) {
        cp'bits = 0xFF'bits'shiftr(leadingOnes+1) && leadingByte'bits'to(Bits32);
        contBytes := min((leadingOnes - 1)'to(Size), s.len - i);
        for (_ in 0:contBytes) {
            contOnes := s[i]'bits'countLeadingOnes;
            if (contOnes == 1) {
                cp'bits = cp'bits'shiftl(6) || (0x3Fub'bits && s[i]'bits)'to(Bits32);
            } elif (contOnes != 1 && contOnes <= 4) {
                # premature end of current sequence. Break b/c
                # a new sequence is following
                cp = -1;
                break;
            } else {
                # broken byte.
                cp = -1;
            }
            i++;
        }
    } elif (leadingOnes == 0) {
        ;; Ascii symbol
        cp = leadingByte'to(Int);
    } else {
        ;; Broken byte
        cp = -1;
    }

    if (cp < 0) {
        cp = 0xFFFD;; Replace invalid bytes by replacement char
    }

    return cp'to(Char);
}
