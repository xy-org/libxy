import libc~[Clib{headers=@{"string.h", "stdio.h", "inttypes.h"}}] in c;
import libxy.(memory, error, slice, bitwise);

func cstr~[StrCtor{prefix="c", interpolation=false}] (
    addr: Ptr, size: Usize
)
    addr~[c.char];

;; A Unicode String
;;
;; The string in stored in UTF-8 wich means access to individual chars
;; is only sequential and not random.
*struct Str {
-:
    addr: Ptr~Byte;
    size: pseudo = SizeField{};

    isOwn: pseudo = OwnField{};

    ;; The byte-length of the string as encoding in UTF-8
    ;; + a flag if the memory is owned or not
    packedSize: %Usize'toBits;
}

;; Tag strings that share memory with other strings
*struct Shared~[xyTag := TagCtor{label="shared"}] {}

*func dtor(str: Str) {
    if (str.isOwn) {
        c.free(str.addr);
    }
}

;; Construct a string from memory buffer
;;
;; The memory pointed to by {addr} should have a length of {size},
;; be UTF-8, and NULL terminated.
;;
;; {takeOwnership} controls if we own the string and if we should call free
;; when we are done with it.
;;
;; Two string sharing memory is possible and requires extra caution.
;; Consider labelling such strings {Shared} in order to add a layer of
;; static type safety.
*func str~[StrCtor{prefix="", interpolation=false}] (
    addr: Ptr, size: Usize, takeOwnership := false,
) Str{
    addr=addr,
    packedSize=size'toBits'shiftl(1) || (Usize + takeOwnership)'toBits
};


-struct OwnField {}

func get(str: Str, :OwnField) = ( str.packedSize && 1uz'toBits )'to(Usize)'to(Bool);


-struct SizeField {}

func get(str: Str, :SizeField) = str.packedSize'shiftr(1)'to(Usize);

func set(str: Str, :SizeField, new: Usize) =
    str.packedSize = new'toBits'shiftl(1) || (str.packedSize && 1uz'toBits);


func get(s: Str, i: Usize) -> Byte
>> i < s.size
{
    return [(s.addr)~[to=Byte] + i];
}

;; Count the number of unicode code points (chars) in the string
;; Illegal byte sequences are counted as 1 code point
*func count(s: Str) -> Usize {
    res : Usize;
    i : Usize;
    while (i < s.size) {
        leadingOnes := s[i]'bits'countLeadingOnes;
        res++;
        i++;
        if (leadingOnes >= 2 && leadingOnes <= 4) {
            contBytes := min((leadingOnes - 1)'to(Usize), s.size - i);
            for (_ in 0:contBytes) {
                contOnes := s[i]'bits'countLeadingOnes;
                if (contOnes != 1 && contOnes <= 4) {
                    # premature end of current sequence. Break b/c
                    # a new sequence is following
                    break;
                }
                # else if {contOnes} == 1 then we have a valid seq
                # if {contOnes} is > 4 then we have a broken byte and we want
                # to skip it
                i++;
            }
        }
        ;; if {leadingOnes == 0} then ascii otherwise broken byte
    }
    return res;
}

*func get(s: Str, int: (RangeInt, RangeUint, RangeUlong)) -> Str~Shared
>> int.start >= 0 && int.start <= int.end
{
    start := min(int.start'to(Usize), s.size);
    end := min(int.end'to(Usize), s.size);
    return Str{s.addr + start, end - start};
}

;; Return true if string is empty
*func isEmpty(s: Str) s.size == 0;

;; Retrun true if string is not empty
*func any(s: Str) s.size != 0;

*func cmp(str1: Str, str2: Str) -> Int {
    if (str1.size > str2.size) {
        return 1;
    } elif (str1.size < str2.size) {
        return -1;
    }
    return c.strncmp(str1.addr~[c.char], str2.addr~[c.char], str1.size);
}

*func startswith(str: Str, prefix: Str) -> Bool {
    if (prefix.size > str.size) {
        return false;
    }
    strncmpRes: Int = c.strncmp(str.addr~[c.char], prefix.addr~[c.char], prefix.size);
    return strncmpRes == 0;
}

*func endswith(str: Str, suffix: Str) -> Bool {
    if (suffix.size > str.size) {
        return false;
    }
    strncmpRes: Int = c.strncmp(str.addr~[c.char] + (str.size - suffix.size), suffix.addr~[c.char], suffix.size);
    return strncmpRes == 0;
}

-struct NotShared~[xyTag := TagCtor{label="shared"}] {}

*func ensureNullTerminated(str: Str) ensureNullTerminated(str, str..(share, NotShared));
func ensureNullTerminated(str: Str, :NotShared) str;
func ensureNullTerminated(str: Str, :Shared) str'copy();

*func isNullTerminated(str: Str) isNullTerminated(str, str..(shared, NotShared));
func isNullTerminated(str: Str, :NotShared) true;
func isNullTerminated(str: Str, :Shared) false;

*func copy(str: Str) -> Str {
    newmem : Memory;
    newmem'realloc(str.size + 1);
    c.memcpy(newmem.addr, str.addr, str.size);
    [newmem.addr + str.size] = 0;; add terminating \0
    return str(newmem.addr, str.size, true);
}

struct CharIter {
    i: Usize;; byte-offset of the code point after {char} in string
    char: Char;; character at that offset
}

*func get(str: Str, charIter: CharIter) = charIter.char;

;; Iterate of the unicode characters (code points) in {str}
;; Illegal byte sequences are replaced by U+FFFD
*func chars~IterCtor(str: Str) -> str[ CharIter ] {
    iter : CharIter;
    next(str, iter);
    return iter;
}

*func valid(str: Str, iter: CharIter) iter.char'to(Int) >= 0;

*func next(str: Str, iter: mut CharIter) {
    cp := decode(str, iter.i);
    iter.char = cp;
}

func decode(s: Str, i: mut Usize) -> Char {
    if (i >= s.size) {
        return (-1)'to(Char);
    }

    cp : Int;
    leadingByte := s[i];
    leadingOnes := leadingByte'bits'countLeadingOnes;
    i++;

    if (leadingOnes >= 2 && leadingOnes <= 4) {
        cp'bits = 0xFF'bits'shiftr(leadingOnes+1) && leadingByte'bits'to(Bits32);
        contBytes := min((leadingOnes - 1)'to(Usize), s.size - i);
        for (_ in 0:contBytes) {
            contOnes := s[i]'bits'countLeadingOnes;
            if (contOnes == 1) {
                cp'bits = cp'bits'shiftl(6) || (0x3Fub'bits && s[i]'bits)'to(Bits32);
            } elif (contOnes != 1 && contOnes <= 4) {
                # premature end of current sequence. Break b/c
                # a new sequence is following
                cp = -1;
                break;
            } else {
                # broken byte.
                cp = -1;
            }
            i++;
        }
    } elif (leadingOnes == 0) {
        ;; Ascii symbol
        cp = leadingByte'to(Int);
    } else {
        ;; Broken byte
        cp = -1;
    }

    if (cp < 0) {
        cp = 0xFFFD;; Replace invalid bytes by replacement char
    }

    return cp'to(Char);
}

;; Expose the underlying memory managed by Str
;;
;; The memory contains the string UTF-8 encoded. The momory's size is the size
;; in bytes. The existance of a terminating null character can be determined by
;; {isNullTerminated}. If present it is NOT counted in the memory size.
*func memory(s: Str) = Memory{s.addr, s.size};

-struct BytesField {}

;; Expose a pointer to the memory manged by Str
;;
;; The momory is UTF-8 encoded. This method exist simply to provided better
;; interface for access to individual bytes and slices based on byte offsets
*func bytes(s: Str) -> s[ BytesField, ... ] {
    return BytesField;
}

*func get(s: Str, :BytesField) = s.addr;

*func get(
    str: Str, :BytesField,
    range: (RangeByte, RangeUbyte, RangeShort, RangeUshort, RangeInt, RangeUint, RangeLong, RangeUlong),
) -> Str~Shared
>> range.start >= 0
>> range.start'to(Usize) < str.size && range.end'to(Usize) <= str.size
{
    if (range.start <= range.end) {
        return Str~Shared{str.addr + range.start, range'len'to(Usize)};
    } else {
        return Str~Shared{Ptr, 0uz};
    }
}


;; Split by (white)space characters
*func split~IterCtor(str: Str) -> str[ SplitIter ] {
    si: mut = SplitIter{.valid};
    next(str, si);
    return si;
}

*func valid(str: Str, iter: SplitIter) = iter.valid;

*func get(str: Str, iter: SplitIter) = iter.word;

*func next(str: Str, iter: mut SplitIter) {
    istart := do -> (istart: Usize) {
        istart = iter.ichar.i;
        next(str, iter.ichar);
        if (!valid(str, iter.ichar)) {
            !.iter.valid;
            return;
        }
    } while (str[iter.ichar]'space);

    iend := do -> (iend: Usize) {
        iend = iter.ichar.i;
        next(str, iter.ichar);
        if (!valid(str, iter.ichar)) {
            break;
        }
    } while (!str[iter.ichar]'space);

    iter.word = Str~Shared{str.addr + istart, iend - istart};
}

struct SplitIter {
    ichar: CharIter;
    word: Str~Shared;
    valid: Bool;
}

*func space(ch: Char) -> Bool {
    return ch == `\n` || ch == ` `;
}

;; Return true if {needle} is found in {haystack}
*func has(haystack: Str, needle: Str) = haystack'find(needle) >= 0z;


;; Search for {needle} in {haystack}.
-func find(haystack: Str, needle: Str, reverse := false) =
    if (reverse'not) ffind(haystack, needle) else rfind(haystack, needle);

-func ffind(haystack: Str, needle: Str) -> Size {
    if (needle.size == 0) return 0z;; memmem is inconsistent if needle is empty

    location: Ptr~Byte = c.memmem(
        haystack.addr, haystack.size, needle.addr, needle.size
    );
    if (location >= haystack.addr) {
        # TODO use pointer arithmatic
        s := haystack.addr;
        return Size{ inlinec"({location} - {s})" };
    } else {
        return -1z;
    }
}

-func rfind(haystack: Str, needle: Str) -> Size {
    if (needle.size == 0) return 0z;

    res: mut = -1z;

    text: mut = Str{haystack.addr, haystack.size};
    offset: Usize;
    while (true) {
        loc := find(text, needle);
        if (loc >= 0) {
            res = offset'to(Size) + loc;
            text.addr += loc'to(Usize) + 1uz;
            text.size -= loc'to(Usize) + 1uz;
            offset += loc'to(Usize) + 1uz;
        } else {
            break;
        }
    }

    return res;
}
