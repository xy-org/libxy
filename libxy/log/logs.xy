import libxy.(string, fs, error);

;; Disable and compile out messages with level lower than that one
*minLogLevel := -30;

;; Common log levels
*logLevelFatal   := 30;
*logLevelError   := 20;
*logLevelWarn    := 10;
*logLevelInfo    := 0;
*logLevelVerbose := -10;
*logLevelDebug   := -20;
*logLevelTrace   := -30;

*defaultLogLevel := logLevelInfo;

*struct Log {
    ;; The higher the more importaint the log message
    level: Int = logLevelInfo,

    file: File = File{1},
    fileOwned: Bool,
}

*func dtor(log: mut Log) {
    if (!log.fileOwned) {
        log.file.fd = -1;
    }
}

*func fatal(^msg: String, log := [Global~Log]) =
    log(log, logLevelFatal, msg);

*func err(^msg: String, log := [Global~Log]) =
    log(log, logLevelError, msg);

*func warn(^msg: String, log := [Global~Log]) =
    log(log, logLevelWarn, msg);

*func info(^msg: String, log := [Global~Log]) =
    log(log, logLevelInfo, msg);

*func verbose(^msg: String, log := [Global~Log]) =
    log(log, logLevelVerbose, msg);

*func v(^msg: String, log := [Global~Log]) =
    log(log, logLevelVerbose, msg);

*func vv(^msg: String, log := [Global~Log]) =
    log(log, logLevelVerbose - 1, msg);

*func vvv(^msg: String, log := [Global~Log]) =
    log(log, logLevelVerbose - 2, msg);

*func debug(^msg: String, log := [Global~Log]) =
    log(log, logLevelDebug, msg);


func log(log: Log, level: Int, ^msg: String) =
if (log.level <= level) {
    log'doLog(level, msg);
};

# TODO make this private
func doLog(log: Log, level: Int, msg: String) {
    log.file'write(msg);
    log.file'write("\n");
}