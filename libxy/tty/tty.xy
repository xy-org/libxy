;; Functionality related to terminals or tty
;; Mostly basic stuff. You can do pretty colors and query a tty about common
;; properties.

;; Helpful resources:
;;   * https://gist.github.com/ConnerWill/d4b6c776b509add763e17f9f113fd25b
;;   * https://invisible-island.net/xterm/ctlseqs/ctlseqs.html
;;   * https://en.wikipedia.org/wiki/ANSI_escape_code

import libxy.string;
import libxy.flag;

;; Data required to generate escape codes
*struct Esc {
    ;; Control Sequence Introducer section

    cursor: CursorControl;
    clear: ClearMode;
    screenBuffer: ScreenBufferControl;

    ;; If turned on, text pasted into the terminal will be surrounded by
    ;; \033[200~ and \033[201~
    bracketedPaste : bracketedPasteMode;

    ;; Select Graphic Rendition section

    sgrFlags: Uint;

    bold: pseudo          = Flag{1};
    faint: pseudo         = Flag{2};
    italic: pseudo        = Flag{4};
    underline: pseudo     = Flag{8};
    blinking: pseudo      = Flag{16};
    inverse: pseudo       = Flag{32};
    hidden: pseudo        = Flag{64};
    strikethrough: pseudo = Flag{128};
    reset: pseudo         = Flag{256};; Reset any previous color/graphics modes

    bg: Color;
    fg: Color;
}

*def get(esc: Esc, flag: Flag) esc.sgrFlags[flag];

*def set(esc: mut Esc, flag: Flag, on: Bool) esc.sgrFlags[flag] = on;

struct CursorControl {
    up := 0;; move cursor up or down {up} lines
    left := 0;; move cursor left or right {left} columns

    x := 0;; set cursor position to {x} column (1-based)
    y := 0;; set cursor position to {y} line (1-based)

    save := false;; save current cursor position
    restore := false;; restore last saved cursor position
}

struct ScreenBufferControl {
    alternative := false;; enable alternative screen buffer
    normal := false;; restore normal screen buffer
}

struct ClearMode {
    ;; Clear in Screen
    toEndOfScreen := false;
    toBeginningOfScreen := false;
    screen := false;
    screenAndScrollback := false;

    ;; Clear in Line
    toEndOfLine := false;
    toBeginningOfLine := false;
    line := false;
}

struct bracketedPasteMode {
    on: Bool;
    off: Bool;
}

;; Info about a color on the terminals
*struct Color {
    r: Ubyte;
    g: Ubyte;
    b: Ubyte;

    mode : ColorMode;

    black:    pseudo = Enum{30};
    red:      pseudo = Enum{31};
    green:    pseudo = Enum{32};
    yellow:   pseudo = Enum{33};
    blue:     pseudo = Enum{34};
    magenta:  pseudo = Enum{35};
    cyan:     pseudo = Enum{36};
    white:    pseudo = Enum{37};
    default:  pseudo = Enum{39};

    gray:          pseudo = Enum{90};
    brightBlack:   pseudo = Enum{90};; Synonym for gray
    brigthRed:     pseudo = Enum{91};
    brigthGreen:   pseudo = Enum{92};
    brigthYellow:  pseudo = Enum{93};
    brigthBlue:    pseudo = Enum{94};
    brigthMagenta: pseudo = Enum{95};
    brigthCyan:    pseudo = Enum{96};
    brigthWhite:   pseudo = Enum{97};

    index: pseudo = IndexColor8Bit{};
}

;; Used just for a nice syntax around Color.index
struct IndexColor8Bit {}

*def get(esc: Color, enum: Enum) =
    esc.mode.depth4 && esc.r == enum.value'to(%esc.r);

*def get(esc: Color, :IndexColor8Bit) esc.r;

*def set(esc: mut Color, enum: Enum, on: Bool) {
    if (on) {
        esc.mode.depth4 = true;
        esc.r = enum.value'to(%esc.r);
    }
}

*def set(esc: mut Color, :IndexColor8Bit, index: Ubyte) {
    esc.mode.depth8 = true;
    esc.r = index;
}

*struct ColorMode {
    value: Ubyte;

    none: pseudo = Enum{0};
    depth4: pseudo = Enum{1};
    depth8: pseudo = Enum{2};
    depth24: pseudo = Enum{3};
}

*def get(cm: ColorMode, enum: Enum) cm.value[enum];
*def set(cm: mut ColorMode, enum: Enum, on: Bool) cm.value[enum] = on;

*def append(fstr: mut Fstr, esc: Esc, enabled: Bool = true) {
    if (!enabled) return;

    esc'appendSgr(fstr);
    esc'appendCursorControl(fstr);
    esc'appendClearMode(fstr);
    esc'appendScreenBuffer(fstr);
    esc'appendPasteMode(fstr);
}

-def appendSgr(esc: Esc, fstr: mut Fstr) {
    if (esc.sgrFlags == 0 && esc.bg.mode.none && esc.fg.mode.none) return;

    fstr'append("\033[");

    if (esc.reset) fstr'append("0;");
    if (esc.bold) fstr'append("1;");
    if (esc.faint) fstr'append("2;");
    if (esc.italic) fstr'append("3;");
    if (esc.underline) fstr'append("4;");
    if (esc.blinking) fstr'append("5;");
    if (esc.inverse) fstr'append("7;");
    if (esc.hidden) fstr'append("8;");
    if (esc.strikethrough) fstr'append("9;");

    if (esc.fg.mode.depth4) {
        fstr'append(esc.fg.r);
        fstr'append(";");
    } elif (esc.fg.mode.depth8) {
        fstr'append("38;5;");
        fstr'append(esc.fg.index);
        fstr'append(";");
    } elif (esc.fg.mode.depth24) {
        fstr'append("38;2;");
        fstr'append(esc.fg.r);
        fstr'append(";");
        fstr'append(esc.fg.g);
        fstr'append(";");
        fstr'append(esc.fg.b);
        fstr'append(";");
    }

    if (esc.bg.mode.depth4) {
        fstr'append(esc.bg.r + 10);
        fstr'append(";");
    } elif (esc.bg.mode.depth8) {
        fstr'append("48;5;");
        fstr'append(esc.bg.index);
        fstr'append(";");
    } elif (esc.bg.mode.depth24) {
        fstr'append("48;2;");
        fstr'append(esc.bg.r);
        fstr'append(";");
        fstr'append(esc.bg.g);
        fstr'append(";");
        fstr'append(esc.bg.b);
        fstr'append(";");
    }


    fstr'backtrack(1u);; the trailing ';'
    fstr'append("m");
}

-def abs(i: Int) if (i >= 0) i else -i;

-def appendCursorControl(esc: Esc, fstr: mut Fstr) {
    if (esc.cursor.up != 0) {
        fstr @= {
            "\033[", esc.cursor.up'abs,
            if (esc.cursor.up > 0) "A" else "B",
        };
    }
    if (esc.cursor.left != 0) {
        fstr @= {
            "\033[", esc.cursor.left'abs,
            if (esc.cursor.left > 0) "D" else "C",
        };
    }

    if (esc.cursor.x > 0 && esc.cursor.y > 0) {
        fstr @= { "\033[", esc.cursor.y, ";", esc.cursor.x, "H" };
    } elif (esc.cursor.x > 0) {
        fstr @= { "\033[", esc.cursor.x, "G" };
    } elif (esc.cursor.y > 0) {
        # TODO now what ? I
    }

    if (esc.cursor.save) {
        fstr @= "\0337";
    }
    if (esc.cursor.restore) {
        fstr @= "\0338";
    }
}

-def appendClearMode(esc: Esc, fstr: mut Fstr) {
    if (esc.clear.toEndOfScreen) {
        fstr @= "\033[0J";
    }
    if (esc.clear.toBeginningOfScreen) {
        fstr @= "\033[1J";
    }
    if (esc.clear.screen) {
        fstr @= "\033[2J";
    }
    if (esc.clear.screenAndScrollback) {
        fstr @= "\033[3J";
    }

    if (esc.clear.toEndOfLine) {
        fstr @= "\033[0K";
    }
    if (esc.clear.toBeginningOfLine) {
        fstr @= "\033[1K";
    }
    if (esc.clear.line) {
        fstr @= "\033[2K";
    }
}

-def appendScreenBuffer(esc: Esc, fstr: mut Fstr) {
    if (esc.screenBuffer.alternative) {
        fstr @= "\033[?1049h";
    }
    if (esc.screenBuffer.normal) {
        fstr @= "\033[?1049l";
    }
}

-def appendPasteMode(esc: Esc, fstr: mut Fstr) {
    if (esc.bracketedPaste.on) {
        fstr @= "\033[?2004h";
    }
    if (esc.bracketedPaste.on) {
        fstr @= "\033[?2004l";
    }
}
