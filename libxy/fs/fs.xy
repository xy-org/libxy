;; FileSystem and Blocking File IO functionality

import posix~[Clib{headers=@{"unistd.h", "fcntl.h"}}] in c;
import libxy.(string, binary, flag, error);

;; Flags governing how a file is to be opened
*struct OpenFlags {
    value: Int;

    append : pseudo = Flag{c.O_APPEND};

    create : pseudo = Flag{c.O_CREAT};

    directory : pseudo = Flag{c.O_DIRECTORY};

    readOnly : pseudo = Flag{c.O_RDONLY};
    #r : pseudo = readOnly;

    writeOnly : pseudo = Flag{c.O_WRONLY};
    #w : pseudo = writeOnly;

    readWrite : pseudo = Flag{c.O_RDWR};
    #rw : pseudo = readWrite;

    truncate : pseudo = Flag{c.O_TRUNC};
    #trunc : pseudo = truncate;; Shorthand for truncate

    exclusive : pseudo = Flag{c.O_EXCL};
    #excl : pseudo = exclusive;; Shorthand for exclusive
}

*def get(oflags: OpenFlags, flag: Flag) = oflags.value[flag];

*def set(oflags: mut OpenFlags, flag: Flag, on: Bool) oflags.value[flag] = on;

;; Struct representing a file handle.
*struct File {
    fd: Int = -1;

    ;; Whether or not the file descriptor is blocking
    ;; Note - getting/setting this field require a syscall
    blocking : pseudo = BlockingField{};
}

;; Open a file
;; See {OpenFlags} for explanation of what all the toggles mean
* def open(
    path: Str,
    append := false, create := false, directory := false, readOnly := false,
    writeOnly := false, readWrite := false, truncate := false,
    exclusive := false,
    mode := 0644
) = open(path, OpenFlags{
        =append, =create, =directory, =readOnly, =writeOnly, =readWrite,
        =truncate, =exclusive
    }, mode);

;; Open a file
*def open(
    path: Str, flags: OpenFlags, mode := 0644
) -> File | SystemError
>> nullTerminated(path)
{
    fd : Int = c.open(path.addr~[c.char], flags.value, mode);
    if (fd < 0) {
        error SystemError{c.errno};
    }
    return File{fd};
}


;; Create and open a temporary file for reading and writing.
;; When closed the file will auto delete
*def openTmp() -> File | SystemError {
    fd : Int;

inlinec"""
#ifdef O_TMPFILE
    {fd} = open(\"/tmp\", O_TMPFILE | O_RDWR, S_IRUSR | S_IWUSR);
#else
    char _l_var_template_[] = \"/tmp/tmp.XXXXXX\";
    {fd} = mkstemp(_l_var_template_);
    if ({fd} >= 0) unlink(_l_var_template_);
#endif";

    if (fd < 0) {
        error SystemError{c.errno};
    }
    return File{fd};
}

;; Dup {file} i.e. make a shollow copy of the file descriptor
;; No actual bytes get copied on the filesystem
*def copy(file: File) -> File | SystemError {
    newFd : Int = c.dup(file.fd);
    if (newFd < 0) {
        error SystemError{c.errno};
    }
    return File{newFd};
}

*def write(file: File, str: Str) -> void | SystemError {
    total : mut Usize;
    do {
        # TODO use c.ssize_t
        written : c.ssize_t = c.write(
            file.fd, str.addr + total, str.size - total
        );
        if (inlinec"{written} < 0") {
            error SystemError{c.errno};
        }
        inlinec"{total} += {written}";
    } while (total < str.size);
}

*def seek(file: File, offset: (Int, Size)) -> void | SystemError {
    res: c.off_t = c.lseek(file.fd, offset, c.SEEK_SET);
    if (inlinec"{res} == (off_t) -1") {
        error SystemError{c.errno};
    }
}

*def close(file: File) -> void | SystemError {
    if (file.fd >= 0) {
        res: Int = c.close(file.fd);
        if (res != 0) {
            error SystemError{c.errno};
        }
    }
}

;; Read from {file} into {fstr} untile EoF, EAGAIN, EWOULDBLOCK
*def read(file: File, fstr: mut Fstr) -> void | SystemError {
    res : c.ssize_t;
    do {
        fstr'ensureCap(4098uz);
        res = c.read(file.fd, fstr.mem.addr + fstr.fill, fstr'capacity);
        if (inlinec"{res} < 0") {
            errno : Int = c.errno;
            if (inlinec"{errno} == EAGAIN || {errno} == EWOULDBLOCK") break;
            error SystemError{errno};
        }

        fstr.fill += Usize{inlinec"{res}"};
    } while (inlinec"{res} > 0");
}

;; ...
*def append(fstr: mut Fstr, file: File) = file'read(fstr);

;; Read from {file} exactly {nbyte} bytes into {buf}
;; {file} must be either blocking or have atleast {nbytes} available for read
*def read(file: File, buf: mut Buf, nbyte: Usize) -> void | SystemError {
    res : c.ssize_t;
    remaining: mut Usize = nbyte;
    buf'ensureCap(nbyte);
    do {
        res = c.read(file.fd, buf.mem.addr + buf.fill, remaining);
        if (inlinec"{res} < 0") {
            error SystemError{c.errno};
        }

        buf.fill += Usize{inlinec"{res}"};
        remaining -= Usize{inlinec"{res}"};
    } while (remaining > 0);
}

;; ...
*def append(buf: mut Buf, file: File, nbyte: Usize) = file'read(buf, nbyte);

-struct BlockingField{}

*def get(file: File, :BlockingField) -> Bool | SystemError {
    flags: Int = c.fcntl(file.fd, c.F_GETFL);
    if (flags < 0) {
        error SystemError{c.errno};
    }

    return inlinec"({flags} & O_NONBLOCK) == 0";
}

*def set(file: File, :BlockingField, toggle: Bool) -> void | SystemError {
    flags: mut Int = c.fcntl(file.fd, c.F_GETFL);
    if (flags < 0) {
        error SystemError{c.errno};
    }

    if (toggle) {
        inlinec"{flags} &= ~O_NONBLOCK";
    } else {
        inlinec"{flags} |= O_NONBLOCK";
    }

    err : Int = c.fcntl(file.fd, c.F_SETFL, flags);
    if (err < 0) {
         error SystemError{c.errno};
    }
}

*def dtor(f: File) {
    f'close;
}

*def mkdir(path: Str, mode := 0755, parents := false) -> void | SystemError
>> nullTerminated(path)
{
    c.mkdir(path.addr~[c.char], mode);
}
