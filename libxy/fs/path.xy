import libxy.slice;

;; Separator character used to separate components in a path
;; typically / on unix and \ on windows
pathSeparator := `/`;

;; Strip last component from a filesystem path
;; If input contains no /'s, return empty interval (i.e. empty string)
*def dirname(
    ;; What to strip
    path: Str,
    ;; Separator symbol to tokenize {path} by.
    ;; ascii code-point is required for performance reasons.
    separator: Byte = pathSeparator'to(Byte)
) -> path[ SizeInterval ] {
    dirLen : mut = path'len;

    # strip trailing /
    while (dirLen >= 2 && path[dirLen-1] == separator) dirLen--;

    # find next /
    while (dirLen >= 1 && path[dirLen-1] != separator) dirLen--;

    # skip multiple / separating the dirname from basename
    while(dirLen >= 2 && path[dirLen-1] == separator) dirLen--;

    # finally strip the separating /
    if (dirLen >= 2 && path[dirLen-1] == separator) dirLen--;

    x := 0z:dirLen;
    return x;
}

struct PathStr {
    fstr: Fstr;
}

def path~[StrCtor{prefix="path", interpolation=true, to=Str}](addr: Ptr, len: Size) -> PathStr{
    return PathStr{fstr(addr, len)};
}

*def append(path: mut PathStr, str: Str) {
    if (str'startswith("/")) {
        path.fstr.len = 0;
    }
    path.fstr'append(str);
}

*def append(path: mut PathStr, cstr: Ptr, len: Size) {
    path.fstr'append(cstr, len);
}

*def to(path: mut PathStr, :Str) -> Str {
    return path.fstr'to(Str);
}