import libxy.slice;

;; Separator character used to separate components in a path
;; typically / on unix and \ on windows
pathSeparator := `/`;

;; Strip last component from a filesystem path
;; If input contains no /'s, return empty interval (i.e. empty string)
*func dirname(
    ;; What to strip
    path: String,
    ;; Separator symbol to tokenize {path} by.
    ;; ascii code-point is required for performance reasons.
    separator: Byte = pathSeparator'to(Byte)
) -> path[ RangeSize ] {
    dirLen : mut = path'memory.size;

    # strip trailing /
    while (dirLen >= 2 && path[dirLen-1] == separator) dirLen--;

    # find next /
    while (dirLen >= 1 && path[dirLen-1] != separator) dirLen--;

    # skip multiple / separating the dirname from basename
    while(dirLen >= 2 && path[dirLen-1] == separator) dirLen--;

    # finally strip the separating /
    if (dirLen >= 2 && path[dirLen-1] == separator) dirLen--;

    x := 0uz:dirLen;
    return x;
}

struct PathStr {
    fstr: Fstring;
}

func path~[StrCtor{prefix="path", interpolation=true, to=String}](addr: Ptr, len: Usize) -> PathStr{
    return PathStr{fstr(addr, len)};
}

*func append(path: mut PathStr, str: String) {
    if (str'startswith("/")) {
        path.fstr.fill = 0;
    }
    path.fstr'append(str);
}

*func append(path: mut PathStr, cstr: Ptr, len: Usize) {
    path.fstr'append(cstr, len);
}

*func to(path: mut PathStr, :String) -> String {
    return path.fstr'to(String);
}