import libc~[Clib{headers=@{"stdlib.h", "string.h"}}] in c;
import libxy.slice;
import libxy.error;

struct Memory {
    addr: Ptr~Byte;
    size: Size;
}

;; Tag for a managed region block that needs to be deallocated using {free()}
struct MemManaged {}

;; Tag for a memory region that is externally managed. The question of how to
;; deallocate the memory is left to the user of the tag.
struct MemExt {}

;; Tag for a memory region that is part of the static memory and doesn't
;; require deallocation
struct MemStatic {}

*def realloc(mem: mut Memory, size: Size) -> void | SystemError {
    newMem : Ptr~Byte = c.realloc(mem.addr, size);
    if (newMem == Ptr{}) {
        error SystemError{c.ENOMEM};
    }
    if (size > mem.size) {
        c.memset(newMem + mem.size, 0, size - mem.size);
    }
    mem.addr = newMem;
    mem.size = size;
}

*def free(mem: mut Memory) {
    c.free(mem.addr);
    mem.addr = Ptr{};
    mem.size = 0;
}

*def set(mem: Memory, interval: SizeInterval, ptr: Ptr)
# >> (
#     (ptr + interval'len) <= (mem + interval.start) |
#     (mem + interval.end) <= ptr
# ) ;; assume no overlap
{
    c.memcpy(mem.addr + interval.start, ptr, interval'len);
}