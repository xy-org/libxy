import xy.ctti;
import libxy.binary;
import libc~[Clib{headers=@{"stdlib.h"}}] in c;

;; Generate cryptographically strong random numbers
;;
;; This family of functions generate random number using the RNG capabilities
;; of the underlying OS/platform.
;;
;; In theory the OS should be the best source of randomness as userspace
;; processes don't have an easy and reliable source of entropy.
;;
;; The major disadvantage of these functions is speed. Also they cannot be
;; seeded. They are best used to seed other RNGs, generate cookies, keys, etc.
;;
;; Have a look at libyx.prng if you don't require cryptographically strong
;; randomness (cases include simulations, hashes, etc.)
*struct PlatformRng {}

*def get(rng: pseudo PlatformRng, :Uint) = Uint{c.arc4random()};

*def get(rng: PlatformRng, :Byte  ) = rng'get(Uint)'to(Byte);
*def get(rng: PlatformRng, :Ubyte ) = rng'get(Uint)'to(Ubyte);
*def get(rng: PlatformRng, :Short ) = rng'get(Uint)'to(Short);
*def get(rng: PlatformRng, :Ushort) = rng'get(Uint)'to(Ushort);
*def get(rng: PlatformRng, :Int   ) = rng'get(Uint)'to(Int);

*def get(rng: PlatformRng, :Ulong) -> (res: Ulong) {
    rng'generate(res'addrof, res'sizeof);
}

*def get(rng: PlatformRng, :Long) = rng'get(Ulong)'to(Long);

*def generate(rng: pseudo PlatformRng, buf: Ptr, size: Size) -> void {
    c.arc4random_buf(buf, size);
}

*def append(buf: mut Buf, rng: PlatformRng, size: Size) -> void {
    buf'ensureCap(size);
    rng'generate(buf.mem.addr + buf.fill, size);
    buf.fill += size;
}