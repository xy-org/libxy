import xy.ctti;
import libxy.(unit, memory, slice, error);
import libc~[Clib{}] in c;

;; A dynamic array. Can grow and shrink as you append and pop elements.
*struct Array~[elemType: struct] {
    mem: Memory;
    len: Usize~Count;
}

*func dtor(arr: mut Array) = -> void {
    for (i in :arr'len) {
        arr[i] =< void;
    }
    arr.mem'free();
};

*func len(a: Array) = a.len;

func ensureCapacity(
    arr: mut Array,
    requiredCap: Usize,
    elemSize : Usize = sizeof(arr..elemType)
) {
    minRequiredSize := requiredCap * elemSize;
    if (arr.mem.size < minRequiredSize) {
        arr.mem'realloc(max(minRequiredSize, 2 * arr.mem.size));
    }
}

*func append(
    arr: Array, elem: don Any,
    elemPtr: Ptr = elem'addrof, elemSize: Usize = elem'sizeof,
)
>> assumeCompatibleTypes(arr..elemType, %elem)
{
    arr'ensureCapacity(arr.len + 1, elemSize=elemSize);
    c.memcpy(arr.mem.addr + arr.len * elemSize, elemPtr, elemSize);
    arr.len++;
}

*func pop(arr: mut Array) = if (arr.len > 0) {
    arr[arr.len-1] =< void;
    arr.len--;
};

*func get(
    arr: Array, i: (Int, Uint, Long, Ulong),
    elemSize : Usize = sizeof(arr..elemType)
) -> arr[ Ptr~[^arr..elemType] ]
>> i'to(Usize) < arr'len
{
    return (arr.mem.addr + i'to(Usize) * elemSize)~void;
}

struct ArrayIter {
    idx: Usize;
}

*func iter(arr: Array) -> arr[ ArrayIter ] {
    return ArrayIter{};
}

*func valid(arr: Array, iter: ArrayIter) = iter.idx < arr'len;

*func next(arr: pseudo Array, iter: mut ArrayIter) {
   iter.idx++;
}

*func get(
    arr: Array, iter: ArrayIter, elemSize : Usize = sizeof(arr..elemType)
) -> arr[ Ptr~[^arr..elemType] ] {
    return (arr.mem.addr + iter.idx * elemSize)~void;
}

*func fill(arr: mut Array, n: Usize, value: Any = (arr..elemType){}) =
    for (i in :n) {
        arr'append(value);
    };

;; Clear the array
*func clear(arr: mut Array) = -> void {
    for (i in :arr'len) {
        arr[i] =< void;
    }
    arr.len = 0;
};

*func sort(
    arr: mut Array, decreasing := false, stable := true,
) = sort(arr, ^$cmp(arr..elemType, arr..elemType), =decreasing, =stable);

*func sort(
    arr: mut Array, cmp : FuncDef, decreasing := false, stable := true,
) =
    if (!decreasing) {
        cmpCb :=(
            func indirectCmp(
                a: Ptr, b: Ptr, cmp := cmp, elemType := arr..elemType
            ) = [cmp]([a~[%elemType]], [b~[%elemType]])
        )'toCallback((:Ptr, :Ptr)->Int);

        if (stable) {
            auxMem: Memory;
            auxMem'realloc(arr'len * sizeof(arr..elemType));
            sortStable(arr.mem.addr, auxMem.addr, arr'len, cmpCb, sizeof(arr..elemType));
            auxMem'free();
        } else {
            sortUnstable(arr.mem.addr, arr'len, cmpCb, sizeof(arr..elemType));
        }
    } else {
        cmpCb := (
            func indirectCmp(
                a: Ptr, b: Ptr, cmp := cmp, elemType := arr..elemType
            ) = -[cmp]([a~[%elemType]], [b~[%elemType]])
        )'toCallback((:Ptr, :Ptr)->Int);

        if (stable) {
            auxMem: Memory;
            auxMem'realloc(arr'len * sizeof(arr..elemType));
            sortStable(arr.mem.addr, auxMem.addr, arr'len, cmpCb, sizeof(arr..elemType));
            auxMem'free();
        } else {
            sortUnstable(arr.mem.addr, arr'len, cmpCb, sizeof(arr..elemType));
        }
    };

-func sortUnstable(
    arr: Ptr~Byte, nelems: Usize, cmp: (:Ptr, :Ptr)->Int, esize: Usize
) {
    # Introsort implementation: hybrid of quicksort insertion sort for small regions
    introsort(arr, nelems, cmp, esize);
}

-func introsort(arr: Ptr~Byte, nelems: Usize, cmp: (:Ptr, :Ptr)->Int, esize: Usize) {
    if (nelems <= 16) {
        arr'insertionSort(nelems, cmp, esize);
        return;
    }
    # Partition

    mid := nelems / 2uz;
    if ([cmp](arr + mid * esize, arr) < 0) {
        unsafeSwap(arr, 0uz, mid, esize);
    }
    if ([cmp](arr + (nelems-1) * esize, arr) < 0) {
        unsafeSwap(arr, 0uz, nelems-1, esize);
    }
    if ([cmp](arr + (nelems-1)*esize, arr + mid*esize) < 0) {
        unsafeSwap(arr, mid, nelems-1, esize);
    }
    # median is in arr[mid]
    pivotMem: Memory;
    pivotMem'realloc(esize);
    c.memcpy(pivotMem.addr, arr + mid * esize, esize);

    lt : mut = 0uz;
    eq : mut = 0uz;
    gt : mut = nelems;
    while (eq < gt) {
        cmpRes := [cmp](arr + eq*esize, pivotMem.addr);
        if (cmpRes < 0) {
            unsafeSwap(arr, eq, lt, esize);
            lt++;
            eq++;
        } elif (cmpRes > 0) {
            unsafeSwap(arr, eq, gt-1, esize);
            gt--;
        } else {
            eq++;
        }
    }
    pivotMem'free();

    if (lt > 0) {
        introsort(arr, lt, cmp, esize);
    }
    if (gt < nelems) {
        introsort(arr + gt * esize, nelems - gt, cmp, esize);
    }
}


-func sortStable(
    arr: Ptr~Byte, aux: %arr, nelems: Usize, cmp: (:Ptr, :Ptr)->Int, esize: Usize
) {
    ;; merge sort falling back to insertion sort for small arrays
    ;; 16 was chosen completely at random.
    if (nelems <= 16) {
        insertionSort(arr, nelems, cmp, esize);
        return;
    }

    mid := nelems / 2uz;
    sortStable(arr, aux, mid, cmp, esize);
    sortStable(arr + mid*esize, aux + mid*esize, nelems-mid, cmp, esize);
    merge(arr, aux, mid, nelems, cmp, esize);
}

func merge(arr: Ptr~Byte, aux: %arr, mid: Usize, nelems: Usize, cmp: (:Ptr, :Ptr)->Int, esize: Usize) {
    left: Ptr~Byte = arr;
    right: Ptr~Byte = arr + mid * esize;
    leftLen : mut = mid;
    rightLen := nelems - mid;
    i : mut = 0uz;
    j : mut = 0uz;
    k : mut = 0uz;
    while (i < leftLen && j < rightLen) {
        firstToCopy : mut = i;
        while (i < leftLen && [cmp](left + i * esize, right + j * esize) <= 0) {
            i++;
        }
        if (firstToCopy < i) {
            c.memcpy(aux + k * esize, left + firstToCopy * esize, (i-firstToCopy)*esize);
            k += i - firstToCopy;
        }
        if (i >= leftLen) break;

        firstToCopy = j;
        j++;; We know there is atleast one to copy
        while (j < rightLen && [cmp](left + i * esize, right + j * esize) > 0) {
            j++;
        }
        if (firstToCopy < j) {
            c.memcpy(aux + k * esize, right + firstToCopy * esize, (j-firstToCopy)*esize);
            k += j - firstToCopy;
        }
    }

    if (i < leftLen) {
        c.memcpy(aux + k * esize, left + i * esize, (leftLen - i) * esize);
        k += leftLen - i;
    }
    if (j < rightLen) {
        c.memcpy(aux + k * esize, right + j * esize, (rightLen - j) * esize);
    }

    c.memcpy(arr, aux, nelems * esize);
}

-func insertionSort(
    arr: Ptr~Byte, nelems: Usize, cmp: (:Ptr, :Ptr)->Int,
    esize: Usize = sizeof(arr..elemType),
) {
    func moveElement(arr: Ptr~Byte, to: Usize, from: Usize, esize: Usize) {
        mem : Memory;
        mem'realloc(esize);
        c.memcpy(mem.addr, arr + from * esize, esize);
        bytesToMove := (from - to) * esize;
        c.memmove(arr + esize * (to + 1), arr + esize * to, bytesToMove);
        c.memcpy(arr + esize * to, mem.addr, esize);
        mem'free();
    }

    ;; the actual insertion sort
    for (i in 1uz:nelems) {
        elem : Ptr = arr + esize * i;
        pos := findInsertionPos(arr, i, cmp, esize, elem);

        if (pos < i) {
            arr'moveElement(to=pos, from=i, =esize);
        }
    }
}

func findInsertionPos(
    arr: Ptr~Byte, nelems: Usize, cmp: (:Ptr, :Ptr)->Int,
    esize: Usize = sizeof(arr..elemType), elem: Ptr,
) -> Usize {
    low : mut = 0uz;
    high : mut = nelems;
    while (low < high) {
        mid := low + ((high - low) / 2uz);
        cmpRes := [cmp](arr + esize * mid, elem);
        if (cmpRes > 0) {
            high = mid;
        } else {
            low = mid + 1;
        }
    }
    return low;
}

func insert(
     arr: mut Array, pos: Usize, elem: pseudo Any,
     elemPtr: Ptr = elem'addrof, esize : Usize = elem'sizeof,
) -> void
>> cmpTypes(arr..elemType, elem) >= 0
>> pos <= arr'len
{
    arr'ensureCapacity(arr.len + 1, elemSize=esize);
    bytesToMove := (arr'len - pos) * esize;
    if (bytesToMove > 0) {
        c.memmove(arr.mem.addr + esize * (pos + 1), arr.mem.addr + esize * pos, bytesToMove);
    }
    c.memcpy(arr.mem.addr + esize * pos, elemPtr, esize);
    arr.len++;
}

*func swap(
    arr: mut Array,
    i: (Byte, Ubyte, Short, Ushort, Int, Uint, Long, Ulong), j: %i,
    esize: Usize = sizeof(arr..elemType)
) -> void
>> 0 <= i && i < arr'len
>> 0 <= j && j < arr'len
{
    if (i != j) {
        unsafeSwap(arr.mem.addr, i'to(Usize), j'to(Usize), esize);
    }
}

-func unsafeSwap(arr: Ptr~Byte, i: Usize, j: Usize, esize: Usize) {
    mem : Memory;
    mem'realloc(esize);
    c.memcpy(mem.addr, arr + esize * i, esize);
    c.memcpy(arr + esize * i, arr + esize * j, esize);
    c.memcpy(arr + esize * j, mem.addr, esize);
    mem'free();
}


-func lessThan(a: Any, b: Any, cmp: FuncDef, decreasing: Bool) =
    (decreasing && [cmp](a, b) > 0) || (decreasing'not && [cmp](a, b) < 0);
