import xy.sys;
import xytest;
import libxy.(list, string, slice);
import ..;

*func helpMsg1~Test() {
    schema : mut = CliSchema{};
    schema'prog(
        "ProgramName",
        desc="What the program does",
        epilog="Text at the bottom of help"
    );
    schema'param("filename", .positional);
    schema'param("count", short=`c`, arg="num");
    schema'param("verbose", short=`v`, arg="");

    (equal) f"{schema}", """
    Usage: ProgramName [--help] [-c <num>] [-v] <filename>

    What the program does

    Positional Arguments:
      <filename>

    Options:
      --help
      -c, --count <num>
      -v, --verbose

    Text at the bottom of help
    ";
}

*func helpMsg2~Test() {
    schema := CliSchema {
        'prog("xytest", desc="Discover and Run Tests"),
        'param("dir", .positional),
    };

    (equal) f"{schema}", """
    Usage: xytest [--help] <dir>

    Discover and Run Tests

    Positional Arguments:
      <dir>

    Options:
      --help
    ";
}

*func autoHelp~Test() {
    schema := CliSchema {
        'param("filename", .positional),
    };

    cliArgs := @{c"./a.out", c"--help"};
    err := | schema'parse(
        rawArgs=CmdArgs{cliArgs'len'to(Int), cliArgs'addrof
    }) |;

    (assert) err.msg'startswith("Usage");
}

*func parsingOnePosArg~Test() {
    schema := CliSchema {
        'prog("ProgramName"),
        'param("filename", .positional),
    };

    cliArgs := @{c"./a.out", c"myfile.txt"};
    parsed := schema'parse(rawArgs=CmdArgs{cliArgs'len'to(Int), cliArgs'addrof});

    (equal) parsed'len, 1;
    (equal) parsed[0].name, "filename";
    (equal) parsed[0].value, "myfile.txt";
}

*func parsingTwoPositional~Test() {
    schema := CliSchema {
        'prog("ProgramName"),
        'param("fileA", .positional),
        'param("fileB", .positional),
    };

    cliArgs := @{c"./a.out", c"file1.txt", c"file2.txt"};
    parsed := schema'parse(rawArgs=CmdArgs{cliArgs'len'to(Int), cliArgs'addrof});

    (equal) parsed'len, 2;
    (equal) parsed[0].name, "fileA";
    (equal) parsed[1].name, "fileB";
    (equal) parsed[0].value, "file1.txt";
    (equal) parsed[1].value, "file2.txt";
}

func lsSchema() = CliSchema {
    'prog("ls"),
    'param("long", `l`, arg=""),
    'param("all", `a`, arg=""),
    'param("reverse", `r`, arg=""),
    'param("files", .positional, nargs=0:, .greedy),
};

*func parsingLs1~Test() {
    schema := 'lsSchema;

    cliArgs := @{c"ls", c"-lr"};
    # TODO cliArgs := @{c"ls", c"-lr", "."};
    # TODO cliArgs := @{c"ls", c".", c"-lr"};
    parsed := schema'parse(rawArgs=CmdArgs{cliArgs'len'to(Int), cliArgs'addrof});

    (equal) parsed'len, 2;
    (equal) parsed[0].name, "long";
    (equal) parsed[1].name, "reverse";
    (equal) parsed[0].value, "";
    (equal) parsed[1].value, "";

    altForm := @{c"ls", c"-l", c"-r"};
    alt := schema'parse(rawArgs=CmdArgs{altForm'len'to(Int), altForm'addrof});

    (equal) alt'len, parsed'len;
    for (i in :alt'len) {
        (equal) alt[i].name, parsed[i].name;
        (equal) alt[i].value, parsed[i].value;
    }
}

*func parsingLs2~Test() {
    schema := 'lsSchema;

    cliArgs := @{c"ls", c"-lr", c"."};
    # cliArgs := @{c"ls", c".", c"-lr"};
    parsed := schema'parse(rawArgs=CmdArgs{cliArgs'len'to(Int), cliArgs'addrof});

    (equal) parsed'len, 3;
    (equal) parsed[0].name, "long";
    (equal) parsed[1].name, "reverse";
    (equal) parsed[2].name, "files";
    (equal) parsed[0].value, "";
    (equal) parsed[1].value, "";
    (equal) parsed[2].value, ".";
}

*func parsingLs3~Test() {
    schema := 'lsSchema;

    cliArgs := @{c"ls", c".", c"-lr"};
    parsed := schema'parse(rawArgs=CmdArgs{cliArgs'len'to(Int), cliArgs'addrof});

    (equal) parsed'len, 2;
    (equal) parsed[0].name, "files";
    (equal) parsed[1].name, "files";
    (equal) parsed[0].value, ".";
    (equal) parsed[1].value, "-lr";
}

*func parsingLs4~Test() {
    schema := 'lsSchema;

    cliArgs := @{c"ls", c"-l", c".", c"-ar"};
    parsed := schema'parse(rawArgs=CmdArgs{cliArgs'len'to(Int), cliArgs'addrof});

    (equal) parsed'len, 3;
    (equal) parsed[0].name, "long";
    (equal) parsed[1].name, "files";
    (equal) parsed[2].name, "files";
    (equal) parsed[1].value, ".";
    (equal) parsed[2].value, "-ar";
}

-func gccLikeSchema() = CliSchema {
    ;; Note: not all of gcc idiosyncrasies can be exactly represented
    ;; -W<warnings> or -L<dir> cannot be represented

    'config(prefixLong="-", prefixShort="-"),
    'param("filename", .positional, nargs=1:2),
    'param("compile-only", short=`c`, arg=""),
    'param("output", short=`o`, arg="file"),
    'param("warning", short=`W`, arg="warning"),
};

*func parsingGccLike1~Test() {
    schema := 'gccLikeSchema;

    cliArgs := @{c"cc", c"program.c", c"-c", c"-o", c"prg.o", c"-W", c"all"};
    parsed := schema'parse(rawArgs=CmdArgs{cliArgs'len'to(Int), cliArgs'addrof});

    (equal) parsed'len, 4;
    (equal) parsed[0].name, "filename";
    (equal) parsed[0].value, "program.c";
    (equal) parsed[1].name, "compile-only";
    (equal) parsed[2].name, "output";
    (equal) parsed[2].value, "prg.o";
    (equal) parsed[3].name, "warning";
    (equal) parsed[3].value, "all";
}

#-func gitSchema() -> CliSchema {
#    schema : CliSchema;
#    schema'param("version", arg="");
#    schema'param("command", .command);
#    schema'command("push");
#    schema'param("repository", .positional, nargs=0:2);
#    schema'param("refspec", .positional, nargs=0:2);
#    schema'command("stash");
#    return schema;
#}
#
#*func parsingGit1~Test() {
#    schema := 'gitSchema;
#
#    cliArgs := @{c"git", c"push", c"origin", c"master"};
#    parsed := schema'parse(rawArgs=CmdArgs{cliArgs'len'to(Int), cliArgs'addrof});
#
#    (equal) parsed'len, 3;
#    (equal) parsed[0].name, "command";
#    (equal) parsed[0].value, "push";
#    (equal) parsed[1].name, "repository";
#    (equal) parsed[1].value, "origin";
#    (equal) parsed[2].name, "refspec";
#    (equal) parsed[2].value, "master";
#}

-func ffmpegSchema() = CliSchema {
    'config(prefixLong="-", prefixShort="-"),
    'param("input", `i`, nargs=1:),
    'param("output", .positional),
    'param("map", nargs=0:),
    'param("-c:a"),
};

func parsingFfmpeg~Test() {
    #testParsing(
    #    'ffmpegSchema,
    #    @{
    #        c"ffmpeg", c"-i", c"A.avi", c"-i", c",B.mp4", c"out1.mkv", c"out2.wav",
    #        c"-map", c"1:a", c"-c:a", c"copy", c"out3.mov"
    #    },
    #    @{
    #        Arg{},
    #        Arg{},
    #        Arg{},
    #        Arg{},
    #        Arg{},
    #        Arg{},
    #        Arg{},
    #    },
    #);
    schema := 'ffmpegSchema();

    cliArgs := @{
        c"ffmpeg", c"-i", c"A.avi", c"-i", c",B.mp4", c"out1.mkv", c"out2.wav",
        c"-map", c"1:a", c"-c:a", c"copy", c"out3.mov"
    };
    parsed := schema'parse(rawArgs=CmdArgs{cliArgs'len'to(Int), cliArgs'addrof});

    (equal) parsed'len, 7;
}

#-func testParsing(
#    schema: CliSchema,
#    args: pseudo @Ptr[],
#    expected: pseudo @Arg[],
#
#    argv: Ptr = args'addrof,
#    argc: Int = args'len'to(Int),
#
#    expectedPtr: Ptr = args'addrof,
#    expectedNum: Size = expected'len,
#) {
#    parsed := schema'parse(rawArgs=CmdArgs{argc, argv});
#
#    (equal) parsed'len, expectedNum;
#}

*func parsingOnePosArgErr~Test() {
    schema := CliSchema {
        'prog("ProgramName"),
        'param("filename", .positional),
    };

    cliArgs := @{c"./a.out"};

    err := | schema'parse(CmdArgs{cliArgs'len'to(Int), cliArgs'addrof}) |;
    (assert) err'to(Bool), "Parsing didn't fail despite missing pos arg";

    (equal) err.msg, "Missing positional parameter 'filename'";
}
