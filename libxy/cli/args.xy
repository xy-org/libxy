import libc~[Clib{headers=@{"string.h"}}] in c;
import xy.sys;
import libxy.(string, stdio, list, slice, memory);

*struct CliSchema {
    name: Str~MemManaged;
    desc: Str~MemManaged;
    epilog: Str~MemManaged;
    short: Str~MemManaged;

    named: List~CliParam;
    positional: List~CliParam;

    prefixLong: Str~MemManaged;
    prefixShort: Str~MemManaged;

    noShortGrouping: Bool;
    manualHelp: Bool;
}

*struct CliParam {
    name: Str~MemManaged;
    desc: Str~MemManaged;
    arg: Str~MemManaged;
    short: Char;
    nargs: RangeInt;
    positional: Bool;
    greedy: Bool;
    command: Bool;
}

*func config(
    schema: mut CliSchema, prefixLong: Str = "--", prefixShort: Str = "-",
    manualHelp := false
) {
    schema .= {
        prefixLong=prefixLong'copy, prefixShort=prefixShort'copy, =manualHelp,
        noShortGrouping= prefixLong != prefixShort,
    };
}

*func prog(
    schema: mut CliSchema, name: Str = "", short: Str = "", desc: Str = "", epilog: Str = "",
) {
    schema.name =< name'copy;
    schema.desc =< desc'copy;
    schema.epilog =< epilog'copy;
    schema.short =< short'copy;
    schema.named'append(CliParam{"help"'copy, desc="show this help message and exit"'copy});
}

*func param(
    schema: mut CliSchema,
    name: Str = "",
    short := Char{},
    desc: Str = "",
    arg: Str = "value",
    positional := false,
    nargs : RangeInt = if (positional) 1:2 else 0:1,
    greedy := false,
    command := false,
) {
    field := CliParam{
        name=name'copy, =short, desc=desc'copy, arg=arg'copy, =nargs, =greedy, =command
    };

    if (positional) {
        schema.positional'append(field =>);
    } else {
        schema.named'append(field =>);
    }
}

func command(
    schema: mut CliSchema,
    name: Str,
    desc := "",
) {
    # TODO implement
}

*func append(fstr: mut Fstr, schema: CliSchema) {
    if (schema.short'any) {
        fstr @= { schema.name, schema.short, "\n\n" };
    }

    fstr @= { "Usage: ", schema.name };

    for (param in schema.named) {
        fstr @= " ";
        if (param.nargs.start == 0) {
            fstr @= "[";
        }
        if (param.short != Char{}) {
            fstr @= { "-", param.short };
        } else {
            fstr @= { "--", param.name };
        }
        if (param.arg'any) {
            fstr @= { " <", param.arg, ">" };
        }
        if (param.nargs.start == 0) {
            fstr @= "]";
        }
    }
    for (param in schema.positional) {
        fstr @= " ";
        if (param.nargs.start == 0) {
            fstr @= "[";
        }
        fstr @= { "<", param.name, ">" };
        if (param.nargs.start == 0) {
            fstr @= "]";
        }
    }
    fstr @= "\n";

    if (schema.desc.size > 0) {
        fstr @= { "\n", schema.desc, "\n" };
    }

    ;; print positional
    if (schema.positional'len > 0) {
        fstr @= "\nPositional Arguments:\n";
    }
    for (param in schema.positional) {
        fstr @= "  ";
        if (param.arg.size > 0) {
            fstr @= { "<", param.name, ">" };
        }
        if (param.desc.size > 0) {
            fstr @= { " - ", param.desc };
        }
        fstr @= "\n";
    }

    ;; print named a.k.a. options
    if (schema.named'len > 0) {
        fstr @= "\nOptions:\n";
    }
    for (param in schema.named) {
        fstr @= "  ";
        if (param.short != Char{}) {
            fstr @= { "-", param.short };
            if (param.name'any) {
                fstr @= ", ";
            }
        }
        if (param.name'any) {
            fstr @= { "--", param.name };
        }
        if (param.arg.size > 0) {
            fstr @= { " <", param.arg, ">" };
        }
        fstr @= "\n";
    }

    ;; epilog if any
    if (schema.epilog.size > 0) {
        fstr @= { "\n", schema.epilog, "\n" };
    }
}

struct ParsingError {
    msg: Str~MemManaged;
}

*func to(pe: ParsingError, :Bool) pe.msg.size > 0;

*func unhandled(pe: ParsingError) {
    print(pe.msg);
    if (!pe.msg'endswith("\n")) {
        print("\n");
    }
}

*func parse(
    schema: CliSchema, rawArgs := [Global~CmdArgs]
) -> List~CliArg | ParsingError {
    stream : List~CliArg;

    numPositional := schema.positional'len;
    numNamed := schema.named'len;

    nargsPositional : List~Int;
    nargsPositional'fill(numPositional);
    nargsNamed: List~Int;
    nargsNamed'fill(numNamed);

    ;; index of current positional param
    posParamIdx: Size;
    greedyPosParam: Bool;
    ;; >= 0 if we have processed a named param and now waiting for its value
    namedParamIdx: mut = -1;


    for (i in 1:rawArgs.argc) {
        tmp : Ptr = rawArgs.argv[i];
        arg := Str~MemExt{tmp, c.strlen(rawArgs.argv[i])};

        if (!greedyPosParam && arg'startswith("--")) {
            ;; long form

            if (!schema.manualHelp && arg=="--help") {
                error ParsingError{ f"{schema}" };
            }

            if (namedParamIdx >= 0) {
                error ParsingError{f"Missing value for {schema.named[namedParamIdx].name}"};
            }
            name := arg[2:];

            paramIdx := findParam(schema, name);
            if (paramIdx < 0) {
                error ParsingError{f"Unknown argument {arg}"};
            }

            if (schema.named[paramIdx].arg'empty) {
                stream @= CliArg {
                    name=schema.named[paramIdx].name'copy,
                    value=""~MemExt,
                    # dataIdx=schema.named[paramIdx].dataIdx,
                };
                nargsNamed[paramIdx]++;
            } else {
                namedParamIdx = paramIdx;
            }
        } elif (!greedyPosParam && arg'startswith("-")) {
            ;; short form
            for (short in arg[1:]'chars) {
                if (namedParamIdx >= 0) {
                    error ParsingError{f"Missing value for {schema.named[namedParamIdx].name}"};
                }

                paramIdx := findParam(schema, short);
                if (paramIdx < 0) {
                    error ParsingError{f"Unknown argument '-{short}'"};
                }

                if (schema.named[paramIdx].arg'empty) {
                    stream'append(CliArg{
                        name=schema.named[paramIdx].name'copy,
                        value=""~MemExt,
                        # dataIdx=schema.named[paramIdx].dataIdx,
                    });
                    nargsNamed[paramIdx]++;
                } else {
                    namedParamIdx = paramIdx;
                }
            }
        } elif (namedParamIdx >= 0) {
            ;; value of named
            stream'append(CliArg{
                name=schema.named[namedParamIdx].name'copy,
                value=Str~MemExt{addr=arg.addr, size=arg.size},
                # dataIdx=schema.named[namedParamIdx].dataIdx,
            });
            nargsNamed[namedParamIdx]++;
            if (nargsNamed[namedParamIdx] + 1 >= schema.named[namedParamIdx].nargs.end) {
                namedParamIdx = -1;
            } elif (!schema.named[namedParamIdx].greedy) {
                namedParamIdx = -1;
            }
        } else {
            ;; positional
            if (posParamIdx >= numPositional) {
                error ParsingError{f"Unrecognized positional argument '{arg}'"};
            }

            stream'append(CliArg{
                name=schema.positional[posParamIdx].name'copy,
                value=Str~MemExt{addr=arg.addr, size=arg.size},
                # dataIdx=schema.positional[posParamIdx].dataIdx,
            });
            nargsPositional[posParamIdx]++;
            greedyPosParam = schema.positional[posParamIdx].greedy;
            if (nargsPositional[posParamIdx] + 1 >= schema.positional[posParamIdx].nargs.end) {
                ;; current positional param satisfied. Move to next one.
                posParamIdx++;
                !.greedyPosParam;
            }
        }
    }

    ;; Check for unsatisifed params
    for (i in :numPositional) {
        if (nargsPositional[i] < schema.positional[i].nargs.start) {
            error ParsingError{
                f"Missing positional parameter '{schema.positional[i].name}'"
            };
        }
    }

    for (i in :numNamed) {
        if (nargsNamed[i] < schema.named[i].nargs.start) {
            error ParsingError{
                f"Missing parameter '{schema.named[i].name}'"
            };
        }
    }

    return stream;
}

*struct CliArg {
    name: Str~MemManaged;
    value: Str~MemExt;
    # dataIdx: Size;
}

-func findParam(schema: CliSchema, name: Str) -> Int {
    for (idx in :, param in schema.named) {
        if (param.name == name) {
            return idx'to(Int);
        }
    }
    return -1;
}

-func findParam(schema: CliSchema, short: Char) -> Int {
    for (idx in :, param in schema.named) {
        if (param.short == short) {
            return idx'to(Int);
        }
    }
    return -1;
}
